plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

subprojects {
    group 'com.leecampbell.cod'
    version '1.0.0'

    apply plugin: 'java'
    apply plugin: 'jacoco'
    
    sourceCompatibility = 1.11

    repositories {
        jcenter()
    }

    jacocoTestReport {
        reports {
            xml.enabled true    //Required by tools (e.g. Coveralls and IDE integrations)
            html.enabled true   //Human readable
        }
    }

    //automatically run the code coverage report. Generated report at domain/build/reports/jacoco/test/html/index.html
    test.finalizedBy jacocoTestReport

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

coveralls {	
  jacocoReportPath = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"	
}	

tasks.coveralls {	
  group = 'Coverage reports'	
  description = 'Uploads the coverage report to Coveralls.io'	
	
  onlyIf { System.env.'CI' }	
}